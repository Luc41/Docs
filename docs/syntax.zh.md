---
title: "语法快速参考"
date: 2019-12-11T16:59:07+11:00
tags: []

toc: true
menu:
  docs:
    weight: 110
    title: "语法参考"
---

本文档是一份您可以在Yarn文件中编写的各种语法的快速参考指南。其并非意在成为一个教程，如需教程，请查阅[此处]({{<ref "tutorial.zh.md">}})！如果您想知道如何构造Yarn代码行才能正常工作，那么本文档将是一个有用的参考。

## 基础用法

Yarn是一种为对话设计的编程语言。这意味着它需要遵照一些规则，使得计算机可以理解它。

Yarn文件以节点（nodes）作为分割。每个节点的组成包含多种不同的语句。
通常来说Yarn Spinner对Yarn文件中的每一新行为视为独立的元素。Yarn Spinner需要理解其可能的含义。

## 节点（nodes）

在Yarn中所有内容都是一个节点，并且您的所有对话包含在一个节点之中。
您的Yarn文件可能包含多个节点或仅有一个节点，但是所有的Yarn都必须是节点的一部分。
所有节点都拥有相同的基础结构：

```yarn
标头
---
正文
===
```

标头与主体之间用`---`分隔（一行上有三个`-`符号）。
节点以`===`（一行上的三个`=`符号）终止。

如果您使用了可视化编辑器，这些节点组成了您在画布上看到的区块。

## 标头（header）

标头包含了节点的元数据（metadata），并且可以根据需要包含任意数量的节点元数据。
元数据通过键-值（key-value）对形式创建。
每个元数据拥有相同的布局：

```yarn
键:值
```

每个元数据必须自己单独占据一行。

这是一个示例：`myKey:myValue`拥有一个`myKey`键和一个对应的`myValue`值。

### 标题（title）

`title`标头定义了节点的名字。所有节点必须存在一个`title`。

此标头看起来将会像是`title:nodename`形式。节点的名字（在这个示例中，节点的名字是`nodename`）在整个项目中必须唯一，并且可以是大小写字母和数字的任意组合。节点标题不能有空格。

这个值是区分大小写的，所以`nodename`与`nodeName`是不同的。

### 标签（tags）

节点可以具有关于它们的其他元数据，您可以在`tags`标头中定义这些元数据。这是一个用空格分隔的单词列表。

不同于元数据`title`，标签的值不要求唯一。

## 正文（body）

正文是节点中`---`和`===`之间的部分，您将会在这个地方花费大量的时间，同时也是所有对话进行的地方。

## 对话（dialogue）

通常情况下您的Yarn文件的主要内容都是对话——游戏中您的角色所说的台词。
对话的每一行都将被Yarn Spinner视为新的对话声明。
对话可以包含几乎任何文本。

大部分对话行的编写方式如下：

```yarn
玩家: 你好，萨丽。
萨丽: 噢！你好。
萨丽: 你偷偷地靠近我。
```
在这个示例中，有角色名在我们想让其说的台词前。
这种形式是可选的，您可以根据需要编写对话：

```yarn
你好，萨丽。
噢！你好。
你偷偷地靠近我。
```

如果您不编写角色名称，那么您的游戏将需要其他方法来确定哪一行对应哪个角色，选择哪种形式将由您自己决定。

### 内联表达式（inline expressions）

对话行中支持嵌入[表达式](#expression)。这些表达式将会在对话运行到此处时展开。在`{`和`}`之内的任何表达式都会被解析并与其他文本一同按序渲染

例如，假设您有一个名为`$gold`的变量。您可以像这样向玩家显示此变量的内容：

```yarn
您有 {$gold} 个金币.
```

使用更复杂的表达式也是支持的。例如，您可以使用自己提供的值进行数学运算：

```yarn
如果您有两倍的金币, 您可以获得 {$gold * 2} 个金块!
```

{{<note>}}
内联表达式可以同[format functions](#format-functions)一同使用，尤其是当您需要对您的文本进行[本地化]({{<ref "localisation.zh.md#localising-inline-expressions">}})的时候。
{{</note>}}

## 选项（options）

选项允许您在不同节点之间移动。

```yarn
[[展示给用户的文本|节点名字]]
```

选项分为两部分，用竖线符号（`|`）分隔。
第一部分是要显示给用户的任何文本，在竖线后面的第二部分是节点的名称。
节点名称必须是Yarn文件中其他位置的另一个节点的名称，否则程序将无法运行。
在玩家选择一个选项后，将加载链接的节点（在本例中为`节点名字`），并且故事将从此处继续。

内联表达式可以在选项中使用。

{{<note>}}
在{{< xref "/docs/writing/controlling.zh" "options" >}}中进一步了解选项。
{{</note>}}



## 跳转（jumps）

跳转告诉Yarn Spinner开始运行某个其他节点。它们不会显示给玩家。

```yarn
[[节点名字]]
```

当Yarn Spinner遇到这一行时，它将立即开始运行`节点名字`节点的内容。

{{<note>}}
在{{< xref "/docs/writing/controlling" "jumps">}}中进一步了解跳转。
{{</note>}}



## 快捷选项（shortcut options）

快捷选项使您可以无需创建新节点来创建简单的分支对话框。
每个快捷选项均以`->`箭头开头，然后是应显示给玩家的一行文本。

```yarn
我们接下来应该去哪？
-> 北
-> 南
-> 东
-> 西
```

这将显示`我们接下来应该去哪？`行，然后显示`北`，`南`，`东`，`西`的选项。

{{<note>}}
在{{< xref "/docs/writing/controlling" "shortcut-options" >}}中进一步了解快捷选项。
{{</note>}}



### 快捷范围（shortcut scope）

快捷选项允许您在其选项下方添加其他语句以运行其他行，并且在许多情况下，这些语句用于设置或更新变量。
新语句必须位于快捷选项的下方，它们是快捷选项的一部分，并且需要相对于该快捷选项缩进。

```yarn
-> 北
    北方很冷。
-> 南
    您已经听说南方的天气晴朗。
-> 东
    您不确定东方强盗的传言是否属实。
-> 西

你继续你的旅程。
```

根据玩家选择的选项将会显示不同的行：

* 在上面的示例中，如果玩家选择`北`选项，对话行`北方很冷`将会再接下来显示，紧接着是`你继续你的旅程`。
* 如果选择`南`或`东`，玩家将会在`你继续你的旅程`之前看到不同的内容。
* 如果选择`西`,玩家将仅能看到`你继续你的旅程`。

缩进必须保持一致，否则Yarn Spinner将无法解析您的意图。

### 快捷条件（shortcut conditionals）

快捷选项也可以限制为仅在需要时显示。
您可以通过在快捷选项的末尾添加`if`语句来完成此操作。

```yarn
-> 上锁的活板门 <<if $hasKey is true>>
-> 北
-> 南
-> 东
-> 西
```
在上面的示例中选项`上锁的活板门`只有在变量`$hasKey`是`true`的情况下才会显示为一个选项并被玩家选择。
如果`$hasKey`为`false`则只有四个方向出现。

## 变量

您可以在变量中存储信息。

变量名是字母和数字的任意组合，以`$`符号开始。
变量名对大小写敏感，因此就Yarn Spinner而言，`$varName`和`$varname`是两个不同的变量。

变量的作用域是全局的，因此他们存在于所有文件的所有节点中。
这意味着你可以在一个文件中声明一个变量，然后另一个文件中使用该变量。

### 值

变量可以包含数字，文本，`true`和`false`或`null`。

#### 赋值

您可以使用`set`操作对变量来设置和更新变量内部的值：

```yarn
<<set $var to 1>>
```

`set`操作的命令的格式为起始符号`<<`，后紧接关键字`set`，之后是变量，在本例中是`$var`，接着是关键字`to`，最终是您期望的值。

如果愿意，也可以使用`=`来替换关键字`to`。

#### 数值

数值不论赋予什么值，其总是带有小数点的（技术上称为浮点数）。
这意味着如果您对一个变量赋值`1`,当您从Yarn Spinner中返回它时将会是`1.0`。

#### 文本

变量内的文本可以是任何您想要的内容，但必须包含引号。 
这意味着`<<set $var to "hello">>`是有效的，`<<set $var to hello>>`不是。

#### `真` 和 `假`

变量可以通过使用关键字`true`、`TRUE`和`false`、`FALSE`来赋值为真或假。
关键字首字母大小写敏感，因此`<<set $var to true>>`是合法的，`<<set $var to True>>`不合法。

## 条件语句

条件语句基于逻辑语句来创建不同的对话和事件分支。
所有条件语句拥有相同的基本结构，一个`if`语句，后接零个或以上的`elseif`语句，继续后接零个或一个`else`语句，最终结束于`endif`。

Yarn Spinner将一直处理条件直到满足条件的部分为止，然后运行并显示该条件的语句。

```yarn
<<if $money > 1>>
	我想买一匹马。
	<<set $money to $money - 2>>
	<<set $hasHorse to true>>
<<elseif $money eq 1 >>
	只要一杯，谢谢。 
	<<set $money to $money - 1>>
<<else>>
	该死，我什么也买不起。
<<endif>>
```

以上为例，如果`$money`正好为`1`则显示`只要一杯，谢谢。 `行，但如果`money`为`5`则显示`我想买一匹马。`行。

条件语句缩进if内的行可能更容易理解，但这不是必需的。

{{<note>}}
在{{< xref "/docs/writing/controlling" >}}中进一步了解条件语句。
{{</note>}}

### `if`, `elseif`, `else` 和 `endif` 

`if`语句会创建一个条件语句，其起始于`<<`关键字，后接`if`关键字和控制`if`语句的表达式，最终终止于关键字`>>`。

如果表达式为真，则显示在`if`与条件语句下一部分之间的任何行，对于`elseif`、`else`或`endif`同理。

#### `elseif`

`elseif`语句是条件语句的可选部分，其作用与`if`相似，但是使用`elseif`关键字。
每个`elseif`结构与`if`结构相同，并且您可以根据需要添加包括零个的任意数量的`elseif`。

```yarn
<<elseif $money eq 1>>
```

#### `else`

`else`语句是条件语句的回调并且它仅在`if`和所有的`elseif`均判断为假时运行。
`else`语句是非必须的。

```yarn
<<else>>
```

#### `endif`

`<<endif>>`语句是必需的，它将结束一个条件语句。
需要该语句是因为Yarn Spinner需要知道条件何时结束。

### 表达式

表达式是一种数学或逻辑运算操作，运行和读起来就像数学式一样。
为了使表达式可以在条件语句中使用，它最终需要判断为真或假。
如果表达式的结果为真，则它将成为要运行的条件语句的一部分。

所有表达式都遵循相同的模式，子表达式、运算符，然后是另一个子表达式。 
如果有需要的话，子表达式可以进一步分解为其他表达式。

```yarn
<<if ($counter + 1) >= ($max - 2)>>
```

#### 逻辑运算符

Yarn Spinner支持一下逻辑运算符并且大多数都有不止一种写法：

- 相等: `eq` 或 `is` 或 `==`
- 不相等: `neq` 或 `!`
- 大于: `gt` 或 `>`
- 小于: `lt` 或 `<`
- 小于等于: `lte` 或 `<=`
- 大于等于: `gte` 或 `>=`
- 布尔或: `or` 或 `||`
- 布尔异或: `xor` 或 `^`
- 布尔非: `!`
- 布尔与: `and` 或 `&&`

#### 数学运算符

- 相加: `+`
- 相减: `-`
- 相乘: `*`
- 相除: `/`
- 相除取余: `%`
- 括号: 使用`(` 开始括号，`)`闭合括号

#### 操作符优先级

Yarn Spinner遵循一个相当标准的操作符优先级顺序，当操作符具有同等优先级时，会从左到右依次执行。
操作符优先级如下：

1. 括号
2. 布尔非
3. 相乘, 相除, 和相除取余
4. 相加, 相减
5. 小于等于, 大于等于, 小于, 大于
6. 相等, 不相等
7. 布尔与, 布尔或, 布尔异或

## 命令

命令是Yarn Spinner向游戏传达已发生的需要处理的事件的一种方式。
这些语句通常用于触发成就并将角色和相机移动到需要的地方。

命令语句起始于命令起始符号`<<`，后接任何您想要传递到游戏的信息，最终结束于命令终止符号`>>`。
举个例子：

```yarn
<<移动 相机 左>>
<<解锁成就 开始冒险>>
```

命令本身不做任何事情，您需要自行处理这些信息。

{{<note>}}
在 {{<xref "/docs/unity/working-with-commands.md">}}中进一步了解如何为您的游戏自定义命令。
{{</note>}}

## 本地化标签

本地化标签是一种为本地化系统标记对话行的语句。
如果您没有对您的游戏进行本地化则您不要该标签，也不会遇到它。
每个标签起始于一个`#`符号紧接一个`line`关键字和一个自动生成的值。

```yarn
#line:a8e70c
```

标签总是位于一行的末尾并且永远不应被手动编辑或创建。
您将会在对话行，快捷选项和选项的末尾找到本地化标签。

```yarn
玩家: 你好。 #line:a8e70c
萨丽: 噢! 你好。 #line:2dc39b

[[回头见。|萨丽.退出]] #line:0facf7
```

## 格式化函数（format functions）

格式化函数允许您根据某个值选择内容。格式化函数在根据变量的值使句子在语法上正确是有用的。

举个例子，如果您想编写句子"I have 1 apple"，单词"apple"在英语中需要根据您所拥有的数量来改变（1 apple or 2 apples），使用格式化函数可以让您根据需要切换这个单词。

格式化函数在[本地化]({{<ref "localisation.md">}})中是十分有用的，因为它在您使用的每个字符表中都可能不同。

格式化函数起始和终止于`[`和`]`。在这些大括号内，您可以指定要使用的特定格式功能，然后指定要使用的类别和替换项的列表。

{{<note>}}
格式化函数语法基于[Unreal](https://docs.unrealengine.com/en-US/Gameplay/Localization/Formatting/#argumentmodifiers), [Unity](https://docs.unity3d.com/Packages/com.unity.localization@0.2/manual/index.html#plural-support), 和Unicode的[MessageFormat](https://messageformat.github.io/messageformat/)中的类似实现。
{{</note>}}

一共有三种可用的格式化函数：`select`，`plural`和`ordinal`。

### `select`

格式化函数`select`允许您使用变量的值在一组固定的选项之间进行选择。

```yarn
[select {$value} option1="replacement1" option2="replacement2"]
```

基于您提供的变量的值，将选择相应的替换项。在上面的示例中，如果`$value`的值为`option1`，则插入文本`"replacement1"`；如果`$value`的值为`option2`，则插入文本`"replacement2"`。替换项的数量没有限制。如果`$value`的值不为任何一个，则插入一个错误值。

{{<note>}}
格式化函数`select`不查找特定值；而是一个通用的文本替换工具。
{{</note>}}

当您想要根据变量保存的角色性别来使用不同的拼写时是非常有用的。

举个例子，想象一下变量`$gender`包含一个角色的性别，并且可能存在值“`m`”对应男性，值“`f`”对应女性，或者“`nb`”对应非二者

```yarn
我没有见到 [select {$gender} m="他" f="她" nb="他们"]。
```

### `plural` 和 `ordinal`

`plural`和`ordinal`格式化函数根据当前语言将数字值映射为它的*复数类*。

不同的语言使用不同的复数形式。在英语（和很多其他语言）中有两种复数类型数：如果一个整数为1，则其为*单数*，其他情况下为*复数*；其他规则适用于不同的语言。

这些格式化函数允许您传递一个变量，并且根据复数类来选择适合的文本显示：

* 格式化函数`plural`选择一个数字的*基本*复数类，其使用在计数值上：“1 apple”，“2 apples”等等。
* 格式化函数`ordinal`选择一个数字的*基本*复数类，其使用在排序值上：“1st”，“2nd”等等。

在`plural`和`ordinal`中可用的复数类有：*zero*，*one*，*two*，*few*，*many*和*other*。

{{<note>}}
不同的语言存在不同的复数类；当本地化包含`plural`或`ordinal`格式函数的行时，您将需要使用适用于目标语言的复数类。
{{</note>}}

用于选择数字的复数类的规则源自[Unicode's CLDR](https://www.unicode.org/cldr/charts/36.1/supplemental/language_plural_rules.html)。在运行时使用的特定规则由{{<xref "/docs/unity/components/dialogue-runner">}}上的文本语言属性决定。

#### `plural`

格式化函数`plural`基于输入值的基本复数类选择文本。

举个例子，想象一下您有一个代表玩家拥有多少个苹果的变量`$apples`，并且您想要将其展示给玩家。此时您就可以像这样使用格式化函数`plural`：

```yarn
You have {$apples} [plural {$apples} one="apple" other="apples"]
```

当[对话运行器]({{<ref "dialogue-runner.md">}})的文本语言设定为英语时，其将会被渲染成下面的样子：

* `$apple == 1`: "You have 1 apple"
* `$apple == 2`: "You have 2 apples"
* `$apple == 0`: "You have 0 apples"

#### `ordinal`

The `plural` format function selects text based on the input value's ordinal plural class.

举个例子，想象一下你有一个包含这玩家在比赛中获得的位置的变量叫做`$race_position`。您可以对该变量使用格式化函数`ordinal`，像下面这样：

```yarn
I came in {$race_position}[ordinal {$race_position} one="st" two="nd" few="rd" other="th"] place!
```

当[对话运行器]({{<ref "dialogue-runner.md">}})的文本语言设定为英语时，其将会被渲染成下面的样子：

* `$race_position == 1`: "I came in 1st place!"
* `$race_position == 2`: "I came in 2nd place!"
* `$race_position == 3`: "I came in 3rd place!"
* `$race_position == 6`: "I came in 6th place!"

### 值注入

在格式化函数的替换值中，`％`字符将用原始值替换用于选择要使用的适当文本。 例如，回到我们的“苹果数量”示例，您可以以仅使数字出现在复数形式的方式编写该行：

```yarn
You have [plural {$apples} one="an apple" other="% apples"]
```

在英语中，它将被渲染成：

* `$apple == 1`: "You have an apple"
* `$apple == 2`: "You have 2 apples"
